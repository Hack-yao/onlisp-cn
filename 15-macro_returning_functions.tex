%!TEX encoding = UTF-8 Unicode
% $Id: 15-macro_returning_functions.tex 17 2014-03-09 13:05:41Z binghe $

\chapter{返回函数的宏}
\label{chap:macro_returning_functions}

第~\ref{chap:returning_functions} 章已经介绍了如何编写返回函数的函数\index{functions 函数!as return values}。宏的应用使得组合操作符这项工作的难度大幅降低。本章将说明如何用宏来构造抽象结构，这些结构和第~\ref{chap:returning_functions}
章里定义的那些抽象是等价的，但是用宏会更清晰和高效。

\section{函数的构造}
\label{sec:building_functions}
\index{macros 宏!for building functions}

若~$f$ 和~$g$ 均为函数，则~$f \circ g(x) = f(g(x))$\index{functions 函数!composition of}。
第~\ref{sec:composing_functions} 节曾介绍过把~$\circ$ 实现为~Lisp 函数的方法，
这个函数名叫~\texttt{compose}：
\begin{lstlisting}
> (funcall (compose #'list #'1+) 2)
(3)
\end{lstlisting}

\begin{figure}
\begin{lstlisting}
(defmacro fn (expr) `#',(rbuild expr))

(defun rbuild (expr)
  (if (or (atom expr) (eq (car expr) 'lambda))
      expr
      (if (eq (car expr) 'compose)
          (build-compose (cdr expr))
          (build-call (car expr) (cdr expr)))))

(defun build-call (op fns)
  (let ((g (gensym)))
    `(lambda (,g)
       (,op ,@(mapcar #'(lambda (f)
                          `(,(rbuild f) ,g))
                      fns)))))

(defun build-compose (fns)
  (let ((g (gensym)))
    `(lambda (,g)
       ,(labels ((rec (fns)
                   (if fns
                       `(,(rbuild (car fns))
                         ,(rec (cdr fns)))
                       g)))
          (rec fns)))))
\end{lstlisting}
  \caption{通用的用于构造函数的宏}
  \label{fig:general_function-building_macro}
  \index{fn@\texttt{fn}}
\end{figure}

在本节中，我们将思考如何用宏来定义更好的函数构造器。\index{functions 函数!set operations on}
图~\ref{fig:general_function-building_macro} 中是一个名为~\texttt{fn} 的通用
函数构造器，它能够根据复合函数的描述来构造它们。它的参数应该是一个形如~\texttt{(\emph{operator} .
\emph{arguments})} 的表达式。\emph{operator} 可以是一个函数或宏的名字
，也可以是会被区别对待的~\texttt{compose}。\emph{Arguments} 可以是接受一个参数的函数
或宏的名字，或者是可作为~\texttt{fn} 参数的表达式。例如，
\begin{lstlisting}
(fn (and integerp oddp))
\end{lstlisting}
产生一个等价于
\begin{lstlisting}
#'(lambda (x) (and (integerp x) (oddp x)))
\end{lstlisting}
的函数。

如果把~\texttt{compose} 用作操作符~(operator)，我们就得到一个所有参数复合后得到的
函数，但不需要像~\texttt{compose} 被定义为函数时那样的显式~\texttt{funcall} 调用。
例如，
\begin{lstlisting}
(fn (compose list 1+ truncate))
\end{lstlisting}
展开成：
\begin{lstlisting}
#'(lambda (#:g1) (list (1+ (truncate #:g1))))
\end{lstlisting}
后者允许对~\texttt{list} 和~\texttt{1+} 这种简单函数进行内联编译。\texttt{fn} 宏
接受一般意义上的操作符名称；\lexpr 也是允许的，就像
\begin{lstlisting}
(fn (compose (lambda (x) (+ x 3)) truncate))
\end{lstlisting}
可以展开成
\begin{lstlisting}
#'(lambda (#:g2) ((lambda (x) (+ x 3)) (truncate #:g2)))
\end{lstlisting}
毫无疑问，这里由~\lexpr 表示的函数会被内联编译，要是换成用~sharp-quoted 的~\lexpr 作为参数，
传给~\texttt{compose}， 那就只得通过~funcall 调用了。

第~\ref{sec:composing_functions} 节还展示了另外三个函数构造器的定义：\texttt{fif}，
\texttt{fint}，以及~\texttt{fun}。这些函数现在被统一到了通用的~\texttt{fn} 宏。
使用~\texttt{and} 操作符将产生一个参数操作符的交集：
\begin{lstlisting}
> (mapcar (fn (and integerp oddp))
          '(c 3 p 0))
(NIL T NIL NIL)
\end{lstlisting}
而~\texttt{or} 操作符则产生并集：
\begin{lstlisting}
> (mapcar (fn (or integerp symbolp))
          '(c 3 p 0.2))
(T T T NIL)
\end{lstlisting}
并且~\texttt{if} 产生的函数，其函数体是条件执行的：
\begin{lstlisting}
> (map1-n (fn (if oddp 1+ identity)) 6)
(2 2 4 4 6 6)
\end{lstlisting}
不过，我们可用的函数不仅仅限于上面三个：
\begin{lstlisting}
> (mapcar (fn (list 1- identity 1+))
          '(1 2 3))
((0 1 2) (1 2 3) (2 3 4))
\end{lstlisting}
并且~\texttt{fn} 表达式里的参数本身也可以是表达式：
\begin{lstlisting}
> (remove-if (fn (or (and integerp oddp)
                     (and consp cdr)))
             '(1 (a b) c (d) 2 3.4 (e f g)))
(C (D) 2 3.4)
\end{lstlisting}

这里虽然~\texttt{fn} 把~\texttt{compose} 作为一种特殊情况单独处理，但是这样做并没有增加这个宏的功能。如果你把嵌套的
参数传给~\texttt{fn}，那就可以得到函数的复合。例如，
\begin{lstlisting}
(fn (list (1+ truncate)))
\end{lstlisting}
展开成：
\begin{lstlisting}
#'(lambda (#:g1)
    (list ((lambda (#:g2) (1+ (truncate #:g2))) #:g1)))
\end{lstlisting}
这相当于
\begin{lstlisting}
(compose #'list #'1+ #'truncate)
\end{lstlisting}
\texttt{fn} 宏把~\texttt{compose} 单独处理的目的，只是为了提高这种调用的可读性。

\section{在~cdr 上做递归}
\label{sec:macro:recursion_on_cdrs}
\index{recursion 递归!on cdrs}

第~\ref{sec:function:recursion_on_cdrs} 和
~\ref{sec:function:recursion_on_subtrees} 节显示了如何编写构造递归函数的函数。
接下来两节将介绍指代宏是如何给我们在那里定义的函数提供一个更清晰的接口的\index{functions 函数!generating recursive}\index{macros 宏!anaphoric 指代!for generating recursive functions 为了生成递归函数}。

第~\ref{sec:function:recursion_on_cdrs} 节显示了如何定义一个称为~\texttt{lrec} 的
扁平列表递归器。通过~\texttt{lrec}，我们可以将下面这个函数：
\begin{lstlisting}
(defun our-every (fn lst)
  (if (null lst)
      t
      (and (funcall fn (car lst))
           (our-every fn (cdr lst)))))
\end{lstlisting}
而把~\texttt{oddp} 的调用表示成：
\begin{lstlisting}
(lrec #'(lambda (x f) (and (oddp x) (funcall f)))
      t)
\end{lstlisting}

在这里，宏可以让我们事半功倍。为了表达一个递归函数\index{lists!recursers on}，最起码应该说清楚哪几件事情呢？如果用~\verb|it| 来指代当前列表的~car，并用~\verb|rec| 指代递归调用，
那么我们就可以把它表示成：
\begin{lstlisting}
(alrec (and (oddp it) rec) t)
\end{lstlisting}
图~\ref{fig:macros_for_list_recursion} 中定义的宏，就允许我们这样做。
\begin{lstlisting}
> (funcall (alrec (and (oddp it) rec) t)
           '(1 3 5))
T
\end{lstlisting}

\begin{figure}
\begin{lstlisting}
(defmacro alrec (rec &optional base)
  "cltl2 version"
  (let ((gfn (gensym)))
    `(lrec #'(lambda (it ,gfn)
               (symbol-macrolet ((rec (funcall ,gfn)))
                 ,rec))
           ,base)))

(defmacro alrec (rec &optional base)
  "cltl1 version"
  (let ((gfn (gensym)))
    `(lrec #'(lambda (it ,gfn)
               (labels ((rec () (funcall ,gfn)))
                 ,rec))
           ,base)))

(defmacro on-cdrs (rec base &rest lsts)
  `(funcall (alrec ,rec #'(lambda () ,base)) ,@lsts))
\end{lstlisting}
  \caption{递归列表的宏}
  \label{fig:macros_for_list_recursion}
  \index{alrec@\texttt{alrec}}
  \index{on-cdrs@\texttt{on-cdrs}}
  \index{symbol-macrolet@\texttt{symbol-macrolet}}
\end{figure}

这个新宏把第二个参数给出的表达式转化成传递给~\texttt{lrec} 的函数，用这种方式实现其功能。
由于第二个参数可能会通过指代引用~\texttt{it} 或~\texttt{rec}，因此，在宏展开式里，函数的主体
所处的作用域必须含有为这些符号建立的绑定。

事实上，图~\ref{fig:macros_for_list_recursion} 中有两个不同版本的~\texttt{alrec}。
前例中使用的版本需要用到符号宏~(symbol macro，见第~\ref{sec:symbol_macros} 节)\index{symbol-macros}\index{Common Lisp!differences between versions!symbol-macros}。
由于只有较新的~Common Lisp 版本才支持符号宏\footnote{译者注：这些问题现在已经不复存在，
几乎所有的现行~Common Lisp 实现~(除了~GCL, GNU Common Lisp) 都支持~ANSI Common
Lisp 标准\pozhehao{}和~\textsc{cltl}2 相比，几乎没有变化。}，所以
图~\ref{fig:macros_for_list_recursion} 里也提供了一个相形之下不那么方便的~\texttt{alrec}
版本，其中~\texttt{rec} 被定义成一个局部函数。代价是，\texttt{rec} 作为函数将不得不
被包在括号里：
\begin{lstlisting}
(alrec (and (oddp it) (rec)) t)
\end{lstlisting}
在支持~\texttt{symbol-macrolet} 的~Common Lisp 实现里，推荐用最初的版本。

Common Lisp 有独立的函数名字空间\index{name-spaces}，这使得通过这些递归构造器定义有名函数略有不便：
\begin{lstlisting}
(setf (symbol-function 'our-length)
      (alrec (1+ rec) 0))
\end{lstlisting}
图~\ref{fig:macros_for_list_recursion} 中最后一个宏的目的就是为了把这一过程变得更抽象一些。
借助~\texttt{on-cdrs}，我们可以只需这样写：
\begin{lstlisting}
(defun our-length (lst)
  (on-cdrs (1+ rec) 0 lst))

(defun our-every (fn lst)
  (on-cdrs (and (funcall fn it) rec) t lst))
\end{lstlisting}

\begin{figure}
\begin{lstlisting}
(defun our-copy-list (lst)
  (on-cdrs (cons it rec) nil lst))

(defun our-remove-duplicates (lst)
  (on-cdrs (adjoin it rec) nil lst))

(defun our-find-if (fn lst)
  (on-cdrs (if (funcall fn it) it rec) nil lst))

(defun our-some (fn lst)
  (on-cdrs (or (funcall fn it) rec) nil lst))
\end{lstlisting}
  \caption{用~\texttt{on-cdrs} 定义的~Common Lisp 函数}
  \label{fig:common_lisp_functions_defined_with_on-cdrs}
  \index{copy-list@\texttt{copy-list}!sketch of}
  \index{remove-duplicates@\texttt{remove-duplicates}!sketch of}
  \index{find-if@\texttt{find-if}!sketch of}
  \index{some@\texttt{some}!sketch of}
\end{figure}

图~\ref{fig:common_lisp_functions_defined_with_on-cdrs} 用这个新宏定义了
几个已有的~Common Lisp 函数。通过使用~\texttt{on-cdrs} 的表达方式，这些函数化简成了它们最
根本的形式，同时，若非这样处理，我们恐怕很难注意到它们间的共同点。

\begin{figure}
\begin{lstlisting}
(defun unions (&rest sets)
  (on-cdrs (union it rec) (car sets) (cdr sets)))

(defun intersections (&rest sets)
  (unless (some #'null sets)
    (on-cdrs (intersection it rec) (car sets) (cdr sets))))

(defun differences (set &rest outs)
  (on-cdrs (set-difference rec it) set outs))

(defun maxmin (args)
  (when args
    (on-cdrs (multiple-value-bind (mx mn) rec
               (values (max mx it) (min mn it)))
             (values (car args) (car args))
             (cdr args))))
\end{lstlisting}
\index{unions@\texttt{unions}}
\index{intersections@\texttt{intersections}}
\index{differences@\texttt{differences}}
\index{maxmin@\texttt{maxmin}}
  \caption{用~\texttt{on-cdrs} 定义的新\utility}
  \label{fig:new_utilities_defined_with_on-cdrs}
\end{figure}

图~\ref{fig:new_utilities_defined_with_on-cdrs} 中有一些新的\utility，
我们可以很方便地用~\texttt{on-cdrs} 来定义它们。前三个分别是：\texttt{unions}，
\texttt{intersections}，和~\texttt{differences}，它们分别实现了集合的并、交、以及差的操作。
虽然~Common Lisp 的内置函数已经实现了这些操作，但它们每次只能用于两个列表。这样的话，如果我们想要找到三个
列表的并集就必须这样写：
\begin{lstlisting}
> (union '(a b) (union '(b c) '(c d)))
(A B C D)
\end{lstlisting}
新的~\texttt{unions} 的行为和~\texttt{union} 相似，但前者能接受任意数量的参数，这样我们
只需说：
\begin{lstlisting}
> (unions '(a b) '(b c) '(c d))
(D C A B)
\end{lstlisting}

%% xxx, the generated entry in idx file always looks like 
%% \indexentry{union@\texttt  {uni on}!unspecified order of result 结果的顺序不确定|hyperpage}
%% so be it
和~\texttt{union}\index{union@\texttt  {union}} 一样，\texttt{unions} 并不保持初始列表中的元素顺
序\index{union@\texttt  {union}!unspecified order of result 结果的顺序不确定}。

在~Common Lisp 的~\texttt{intersection}\index{intersection@\texttt{intersection}} 和更通用的
~\texttt{intersections} 之间也有着同样的关系。在这个函数的定义里，为了改善效率，在最开始的地方加入了对于宏参数的~null 测试；
如果集合中存在空集，它将短路掉整个计算过程。

Common Lisp 也有一个称为~\verb|set-difference|\index{set-difference@\texttt{set-difference}} 的函数，它接受两个列表，并返回属于第一个集合但不属于第二个集合的元素：
\begin{lstlisting}
> (set-difference '(a b c d) '(a c))
(D B)
\end{lstlisting}

我们的新版本处理多重参数的方式和~\texttt{-} 同出一辙。例如，\texttt{(differences x y z)} 就等价
于~\texttt{(set-difference x (unions y z))}，只是不像后者那样需要做~cons。
\begin{lstlisting}
> (differences '(a b c d e) '(a f) '(d))
(B C E)
\end{lstlisting}

这些集合操作符仅仅是作为例子。对于它们没有实际的需要，因为内置
的~\texttt{reduce}\index{reduce@\texttt{reduce}} 已经能处理上面这些例子所示的列表递归的简单情况。例
如，不用
\begin{lstlisting}
(unions ...)
\end{lstlisting}
的话，你也可以说
\begin{lstlisting}
((lambda (&rest args) (reduce #'union args)) ...)
\end{lstlisting}

虽然如此，在一般情况下~\verb|on-cdrs| 比~\verb|reduce| 的功能更强。

因为~\texttt{rec} 指向的是一个调用而非一个值，所以我们可以用~\texttt{on-cdrs} 来创建
返回多值的函数。图~\ref{fig:new_utilities_defined_with_on-cdrs} 中最后一个函数，
\texttt{maxmin}，利用这种可能性在一次列表遍历中同时找出最大和最小的元素：
\begin{lstlisting}
> (maxmin '(3 4 2 8 5 1 6 7))
8
1
\end{lstlisting}

后面章节中的代码也可以用上~\texttt{on-cdrs}。例如，\texttt{compile-cmds}
(第~\pageref{fig:compilation_of_nodes_and_arcs} 页)
\begin{lstlisting}
(defun compile-cmds (cmds)
  (if (null cmds)
      'regs
      `(,@(car cmds) ,(compile-cmds (cdr cmds)))))
\end{lstlisting}
可以简单地定义成：
\begin{lstlisting}
(defun compile-cmds (cmds)
  (on-cdrs `(,@it ,rec) 'regs cmds))
\end{lstlisting}

\section{在子树上递归}
\label{sec:macro:recursion_on_subtrees}
\index{recursion 递归!on subtrees 在子树上}

宏在列表上进行的递归操作，在子树上也一样可以用递归的方式完成。本节里，我们用宏来给
~\ref{sec:function:recursion_on_subtrees} 节里定义的树递归器定义更加清晰的接口。

在~\ref{sec:function:recursion_on_subtrees} 节里我们定义了两个树递归构造器，分别名为~\texttt{ttrav} 和~\texttt{trec}。
前者总是遍历完整棵树；后者功能更为复杂，但它允许你控制递归停止的时机。借助
这些函数，我们可以把~\texttt{our-copy-tree}
\begin{lstlisting}
(defun our-copy-tree (tree)
  (if (atom tree)
      tree
      (cons (our-copy-tree (car tree))
            (if (cdr tree) (our-copy-tree (cdr tree))))))
\end{lstlisting}
表达成
\begin{lstlisting}
(ttrav #'cons)
\end{lstlisting}
而一个对~\texttt{rfind-if}
\begin{lstlisting}
(defun rfind-if (fn tree)
  (if (atom tree)
      (and (funcall fn tree) tree)
      (or (rfind-if fn (car tree))
          (and (cdr tree) (rfind-if fn (cdr tree))))))
\end{lstlisting}
的调用，例如~\texttt{oddp}，可以表达成：
\begin{lstlisting}
(trec #'(lambda (o l r) (or (funcall l) (funcall r)))
      #'(lambda (tree) (and (oddp tree) tree)))
\end{lstlisting}

Anaphoric 宏可以为~\texttt{trec} 做出一个更好的接口，就像前一节中它们对~\texttt{lrec}
所做的那样。要满足一般的需求，这个宏将必须能够同时指代引用到三个东西：当前所在树，我们称之为
~\texttt{it}；递归下降左子树，我们称之为~\texttt{left}；以及递归下降右子树，我们称之为
~\texttt{right}。有了这些约定以后，我们就应该可以像下面这样，用新宏来表达前面的函数：
\begin{lstlisting}
(atrec (cons left right))

(atrec (or left right) (and (oddp it) it))
\end{lstlisting}
图~\ref{fig:macros_for_recursion_on_trees} 包含有这个宏的定义。

\begin{figure}
\begin{lstlisting}
(defmacro atrec (rec &optional (base 'it))
  "cltl2 version"
  (let ((lfn (gensym)) (rfn (gensym)))
    `(trec #'(lambda (it ,lfn ,rfn)
               (symbol-macrolet ((left (funcall ,lfn))
                                 (right (funcall ,rfn)))
                 ,rec))
           #'(lambda (it) ,base))))

(defmacro atrec (rec &optional (base 'it))
  "cltl1 version"
  (let ((lfn (gensym)) (rfn (gensym)))
    `(trec #'(lambda (it ,lfn ,rfn)
               (labels ((left () (funcall ,lfn))
                        (right () (funcall ,rfn)))
                 ,rec))
           #'(lambda (it) ,base))))

(defmacro on-trees (rec base &rest trees)
  `(funcall (atrec ,rec ,base) ,@trees))
\end{lstlisting}
  \caption{在树上做递归的宏}
  \label{fig:macros_for_recursion_on_trees}
  \index{atrec@\texttt{atrec}}
  \index{on-trees@\texttt{on-trees}}
  \index{symbol-macrolet@\texttt{symbol-macrolet}}
\end{figure}

在没有~\texttt{symbol-macrolet} 的~Lisp 版本中，我们可以用图
~\ref{fig:macros_for_recursion_on_trees} 中的第二个定义来定义~\texttt{atrec}。
这个版本将~\texttt{left} 和~\texttt{right} 定义成局部函数，所以
~\texttt{our-copy-tree} 就必须写成：
\begin{lstlisting}
(atrec (cons (left) (right)))
\end{lstlisting}

\begin{figure}
\begin{lstlisting}
(defun our-copy-tree (tree)
  (on-trees (cons left right) it tree))

(defun count-leaves (tree)
  (on-trees (+ left (or right 1)) 1 tree))

(defun flatten (tree)
  (on-trees (nconc left right) (mklist it) tree))

(defun rfind-if (fn tree)
  (on-trees (or left right)
            (and (funcall fn it) it)
            tree))
\end{lstlisting}
  \caption{用~\texttt{on-trees} 定义的函数}
  \label{fig:functions_defined_using_on-trees}
  \index{copy-tree@\texttt{copy-tree}}
  \index{rfind-if@\texttt{rfind-if}}
  \index{flatten@\texttt{flatten}}
\end{figure}

出于便利，我们也定义了一个~\texttt{on-trees} 宏，跟前一节里的~\texttt{on-cdrs} 相似。
图~\ref{fig:functions_defined_using_on-trees} 显示了用~\texttt{on-trees} 定义的
四个在~\ref{sec:function:recursion_on_subtrees} 节里定义的函数。

正如第~\ref{chap:returning_functions} 章里提到的，那一章里的递归生成器构造的函数
将不是尾递归的。用~\texttt{on-cdrs} 或~\texttt{on-trees} 定义出的函数不一定是
最高效的实现。和底层的~\texttt{trec} 和~\texttt{lrec} 一样，这些宏主要用于原型设计以及
效率不是关键的那部分程序里面。尽管如此，本章和第~\ref{chap:returning_functions} 章的核心
思路是：我们可以先编写函数生成器，然后为它们设计出简洁清爽的宏接口。同样的技术也一样可以用在
构造那些能够产生特别高效代码的函数生成器上。

\section{惰性求值}
\label{sec:lazy_evaluation}
\index{evaluation 求值!lazy 惰性}

\emph{惰性求值} 就是说，只有当你需要表达式值的时候，才去求值它。使用惰性求值的方式之一是
构造一种叫~\emph{delay} 的对象。Delay 是某个表达式的值的替代物。它代表着一个承诺\index{promises 承诺}，即：
如果今后需要的话，就要给出表达式的值。同时，由于这个承诺本身是个~Lisp 对象，因而它代表的值所有的功用，
它都责无旁贷，一肩挑起。所以，一旦有需要，delay 就能返回表达式的值。

\begin{figure}
\begin{lstlisting}
(defconstant unforced (gensym))

(defstruct delay forced closure)

(defmacro delay (expr)
  (let ((self (gensym)))
    `(let ((,self (make-delay :forced unforced)))
       (setf (delay-closure ,self)
             #'(lambda ()
                 (setf (delay-forced ,self) ,expr)))
       ,self)))

(defun force (x)
  (if (delay-p x)
      (if (eq (delay-forced x) unforced)
          (funcall (delay-closure x))
          (delay-forced x))
      x))
\end{lstlisting}
  \caption{\texttt{force} 和~\texttt{delay} 的实现}
  \label{fig:implementation_of_force_and_delay}
  \index{delay@\texttt{delay}}
  \index{force@\texttt{force}}
\end{figure}

Scheme 内置了对~delay 的支持。Scheme 的操作符~\texttt{force} 和~\texttt{delay} 就是为此服务的。
用~Common Lisp 的话，可以用图~\ref{fig:implementation_of_force_and_delay} 中的方法来实现这两个操作符。
其中，把~delay 表示成了一个由两部分构成的结构体。第一个字段代表~delay 是否已经被求值了，
如果是的话就被赋予这个值。第二个字段则是一个闭包，调用它就能得到该~delay 所代表的值。
宏~\texttt{delay} 接受一个表达式，并返回一个代表该表达式值的~delay：
\begin{lstlisting}
> (let ((x 2))
    (setq d (delay (1+ x))))
#S(DELAY ...)
\end{lstlisting}

若要调用~delay 里的闭包，就得~\texttt{force} 这个~delay。函数~\texttt{force} 接受
任意对象：对于普通对象它就是~\texttt{identity} 函数，但对于~delay，它是对~delay
所代表的值的请求。
\begin{lstlisting}
> (force 'a)
A
> (force d)
3
\end{lstlisting}
无论何时，只要需要处理的对象有可能是~delay，就应该用~\texttt{force} 对付它。例如，如果我们正在排序的列表
可能含有~delay，那么就要用：
\begin{lstlisting}
(sort lst #'(lambda (x y) (> (force x) (force y))))
\end{lstlisting}

像这样直接用~delay 显得有些笨拙。要是在实际应用中，它们可能会藏身于另一个抽象层之下。

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "onlisp-cn"
%%% End:
